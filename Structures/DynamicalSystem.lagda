\DeclareUnicodeCharacter{7472}{\ensuremath{7472}}

\section{Dynamical Systems: Seemingly Super Simple Automata}

We consider the theory of \emph{dynamical systems} which consist of a type
along with an elected value of that type and a way to obtain new elements.

Think of a box with a screen displaying the current state and a button that alters the state.

Sounds like a computer ;-)

%{{{ Imports
\begin{code}
{-# OPTIONS --allow-unsolved-metas #-}

module Structures.DynamicalSystem where

open import Level renaming (suc to lsuc; zero to lzero)
open import Function using (id ; _âˆ˜_)
open import Data.Maybe using (Maybe; just; nothing; maybe; maybeâ€²)

open import Helpers.Categorical
open import Helpers.Forget
open import Helpers.EqualityCombinators
open â‰¡
\end{code}
%}}}

%{{{ Definition and Hom

\subsection{Definition}

\begin{code}
record DynamicalSystem {a} : Set (lsuc a) where
  constructor MkDS
  field
    States : Set a
    Start  : States
    Next   : States â†’ States

open DynamicalSystem
\end{code}

Unsurprisingly, a ``structure preserving operation'' on such structures is a function
between the underlying carriers that takes the source's point to the target's point.

\begin{code}
record Hom {â„“} (X Y : DynamicalSystem {â„“}) : Set â„“ where
  constructor MkHom
  field
    mor           :  States X â†’ States Y
    preservation  :  mor (Start X) â‰¡ Start Y
    respect       :  mor âˆ˜ Next X â‰áµ¢ Next Y âˆ˜ mor

open Hom
\end{code}
%}}}

%{{{ category
\subsection{Category and Forgetful Functors}

Since there is only one type, or sort, involved in the definition,
we may hazard these structures as ``one sorted algebras'':

\begin{code}
Id : {â„“ : Level} {A : DynamicalSystem {â„“}} â†’ Hom A A
Id = MkHom id refl refl

oneSortedAlg : âˆ€ {â„“} â†’ OneSortedAlg â„“
oneSortedAlg = record
   { Alg         =   DynamicalSystem
   ; Carrier     =   States
   ; Hom         =   Hom
   ; mor         =   mor
   ; comp        =   Î» F G â†’ MkHom (mor F âˆ˜ mor G)
                             (cong (mor F) (preservation G) âŸ¨â‰¡â‰¡âŸ© preservation F)
                             (cong (mor F) (respect G) âŸ¨â‰¡â‰¡âŸ© respect F)
   ; comp-is-âˆ˜   =   â‰-refl
   ; Id          =   Id
   ; Id-is-id    =   â‰-refl
   }
\end{code}

From which we immediately obtain a category and a forgetful functor.

\begin{code}
DS : (â„“ : Level) â†’ Category (lsuc â„“) â„“ â„“
DS â„“ = oneSortedCategory â„“ oneSortedAlg

Forget : (â„“ : Level) â†’ Functor (DS â„“) (Sets â„“)
Forget â„“ = mkForgetful â„“ oneSortedAlg
\end{code}

%}}}

%{{{ Free ; MaybeLeft ; NoRight
\subsection{A Free Construction}

As discussed earlier, the prime example of pointed algebras are the optional types,
and this claim can be realised as a functor:
\begin{code}

{-   DynamicalSystem â‰ˆ Pushout of Pointed and Unary
  â‡’  Free DynamicalSystem â‰ˆ Pushout of Free Pointed and Free Unary
  â‡’  Possibly â‰ˆ Pushout of Maybe and Eventually (â€¢Ì€á´—â€¢Ì)Ùˆ
-}
data Possibly {â„“ : Level} (A : Set â„“) : Set â„“ where
  never  : Possibly A
  indeed : A â†’ Possibly A
  later  : Possibly A â†’ Possibly A

map : âˆ€ {â„“} {A B : Set â„“} â†’ (A â†’ B) â†’ Possibly A â†’ Possibly B
map f never      = never
map f (indeed x) = indeed (f x)
map f (later x)  = later (map f x)

map-id : {â„“ : Level} {A : Set â„“} â†’ map id â‰ id {A = Possibly A}
map-id never      = refl
map-id (indeed x) = refl
map-id (later x)  = cong later (map-id x)

map-âˆ˜ : {â„“ : Level} {A B C : Set â„“} {f : B â†’ C} {g : A â†’ B} â†’ map (f âˆ˜ g) â‰ map f âˆ˜ map g
map-âˆ˜ never      = refl
map-âˆ˜ (indeed x) = refl
map-âˆ˜ (later x)  = cong later (map-âˆ˜ x)

map-cong : {â„“ : Level} {A B : Set â„“} {f g : A â†’ B} â†’ f â‰áµ¢ g â†’ map f â‰ map g
map-cong fâ‰ˆg never      = refl
map-cong fâ‰ˆg (indeed x) = cong indeed fâ‰ˆg
map-cong fâ‰ˆg (later x)  = cong later (map-cong fâ‰ˆg x)

{- eliminator / induction -}
possibly : {a b : Level} {A : Set a} {B : Possibly A â†’ Set b}
         â†’ B never
         â†’ (âˆ€ ix â†’ B (indeed ix))
         â†’ (âˆ€ {lx} â†’ B lx â†’ B (later lx))
         â†’
           (pa : Possibly A)  â†’  B pa
possibly bn bi bl never      = bn
possibly bn bi bl (indeed x) = bi x
possibly {B = B} bn bi bl (later x)  = bl (possibly {B = B} bn bi bl x)

Free : (â„“ : Level) â†’ Functor (Sets â„“) (DS â„“)
Free â„“ = record
  { Fâ‚€             =   Î» A â†’ MkDS (Possibly A) never later
  ; Fâ‚             =   Î» f â†’ MkHom (map f) refl refl
  ; identity       =   map-id
  ; homomorphism   =   map-âˆ˜
  ; F-resp-â‰¡      =   map-cong
  }
\end{code}

Which is indeed deserving of its name:

\begin{code}
AdjLeft : (â„“ : Level) â†’ Adjunction (Free â„“) (Forget â„“)
AdjLeft â„“ = record
  { unit        =   record { Î· = Î» X x â†’ indeed x ; commute = Î» _ â†’ refl }
  ; counit      =   record
    { Î·         =    Î» X â†’ MkHom (possibly (Start X) id (Next X)) refl refl
    ; commute   =    Î» {X} {Y} f â†’ sym âˆ˜ possibly (preservation f) (Î» _ â†’ refl) Î» ind â†’ respect f âŸ¨â‰¡â‰¡âŸ© cong (Next Y) ind
    }
  ; zig         =    possibly refl (Î» _ â†’ refl) (cong later)
  ; zag         =    refl
  }
\end{code}

While there is a ``least'' dynamical system for any given set, there is, in-general, no ``largest'' dynamical system
corresponding to any given set. That is, there is no co-free functor. Indeed, what is the starting state assigned to the empty state space?

\begin{code}
open import Helpers.DataProperties

NoRight : {â„“ : Level} â†’ (CoFree : Functor (Sets â„“) (DS â„“)) â†’ Adjunction (Forget â„“) CoFree â†’ âŠ¥
NoRight {â„“} (record { Fâ‚€ = f }) Adjunct = Î· (counit Adjunct) âŠ¥ (Start (f âŠ¥))
  where open Adjunction
        open NaturalTransformation
\end{code}
%}}}

%{{{ Initial object

Suppose there were an inital object ğ‘°, then we can find maps â€œ! : ğ‘° â†’ Xâ€
with ! (Start ğ‘°)  = Start X
and  ! (Next ğ‘° e) = Next X (! e)

Together these two homomorphism constraints seem to define a function â€œ!â€
which quickly leads to consider ğ‘° = â„•.
Indeed, the intial object is just the free object on the singleton set (â”€â€¿â€¿â”€)

\begin{code}
open import Data.Nat

open import Helpers.DataProperties

initial : Initial (DS lzero)
initial = record
  { âŠ¥         =   MkDS â„• zero suc
  ; !         =   Î»{ {MkDS X start next} â†’ MkHom (foldn start next) refl refl}
  ; !-unique  =   Î» {A} f â†’ sym âˆ˜ foldn (preservation f) Î» ind â†’ respect f âŸ¨â‰¡â‰¡âŸ© cong (Next A) ind
  }
\end{code}

%}}}

Super neat stuffs ^_^

% Quick Folding Instructions:
% C-c C-s :: show/unfold region
% C-c C-h :: hide/fold region
% C-c C-w :: whole file fold
% C-c C-o :: whole file unfold
%
% Local Variables:
% folded-file: t
% eval: (fold-set-marks "%{{{ " "%}}}")
% eval: (fold-whole-buffer)
% fold-internal-margins: 0
% end:
