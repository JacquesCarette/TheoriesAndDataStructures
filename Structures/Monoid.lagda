\section{Monoids: Lists}

%{{{ Imports
\begin{code}
module Structures.Monoid where

open import Level renaming (zero to lzero; suc to lsuc)
open import Data.List using (List; _‚à∑_ ; []; [_]; _++_; foldr; map)
open import Data.List.Properties

open import Categories.Category   using (Category)
open import Categories.Functor    using (Functor)
open import Categories.Adjunction using (Adjunction)
open import Categories.Agda       using (Sets)
open import Function              using (id ; _‚àò_ ; const)
open import Function2             using (_$·µ¢)

open import Forget
open import EqualityCombinators
open import DataProperties

\end{code}
%}}}

%{{{ Some remarks about recursion principles
\subsection{Some remarks about recursion principles}
( To be relocated elsewhere )

\begin{spec}
open import Data.List

rcList : {X : Set} {Y : List X ‚Üí Set} (g‚ÇÅ : Y []) (g‚ÇÇ : (x : X) (xs : List X) ‚Üí Y xs ‚Üí Y (x ‚à∑ xs)) ‚Üí (xs : List X) ‚Üí Y xs
rcList g‚ÇÅ g‚ÇÇ [] = g‚ÇÅ
rcList g‚ÇÅ g‚ÇÇ (x ‚à∑ xs) = g‚ÇÇ x xs (rcList g‚ÇÅ g‚ÇÇ xs)

open import Data.Nat hiding (_*_)

rc‚Ñï : {‚Ñì : Level} {X : ‚Ñï ‚Üí Set ‚Ñì} (g‚ÇÅ : X zero) (g‚ÇÇ : (n : ‚Ñï) ‚Üí X n ‚Üí X (suc n)) ‚Üí (n : ‚Ñï) ‚Üí X n
rc‚Ñï g‚ÇÅ g‚ÇÇ zero = g‚ÇÅ
rc‚Ñï g‚ÇÅ g‚ÇÇ (suc n) = g‚ÇÇ n (rc‚Ñï g‚ÇÅ g‚ÇÇ n)
\end{spec}

Each constructor |c : Srcs ‚Üí Type| becomes an argument |(ss : Srcs) ‚Üí X ss ‚Üí X (c ss)|, more or less :-)
to obtain a ‚Äúrecursion theorem‚Äù like principle.
The second piece |X ss| may not be possible due to type considerations.
Really, the induction principle is just the *dependent* version of folding/recursion!

Observe that if we instead use arguments of the form |{ss : Srcs} ‚Üí X ss ‚Üí X (c ss)| then, for one reason or
another, the dependent type |X| needs to be supplies explicity --yellow Agda! Hence, it behooves us to use explicits
in this case. Sometimes, the yellow cannot be avoided.
%}}}

%{{{ Monoid ; Hom
\subsection{Definition}
\begin{code}
record Monoid ‚Ñì : Set (lsuc ‚Ñì) where
  field
    Carrier   :   Set ‚Ñì
    Id        :   Carrier
    _*_       :   Carrier ‚Üí Carrier ‚Üí Carrier
    leftId    :   {x : Carrier} ‚Üí Id * x ‚â° x
    rightId   :   {x : Carrier} ‚Üí x * Id ‚â° x
    assoc     :   {x y z : Carrier} ‚Üí (x * y) * z ‚â° x * (y * z)

open Monoid

record Hom {‚Ñì} (Src Tgt : Monoid ‚Ñì) : Set ‚Ñì where
  constructor MkHom
  open Monoid Src renaming (_*_ to _*‚ÇÅ_)
  open Monoid Tgt renaming (_*_ to _*‚ÇÇ_)
  field
    mor     :  Carrier Src ‚Üí Carrier Tgt
    pres-Id : mor (Id Src) ‚â° Id Tgt
    pres-Op : {x y : Carrier Src} ‚Üí mor (x *‚ÇÅ y)  ‚â°  mor x *‚ÇÇ mor y

open Hom
\end{code}

%}}}

%{{{ MonoidAlg ; MonoidCat
\subsection{Category}
\begin{code}
MonoidAlg : {‚Ñì : Level} ‚Üí OneSortedAlg ‚Ñì
MonoidAlg {‚Ñì} = record
   { Alg         =   Monoid ‚Ñì
   ; Carrier     =   Carrier
   ; Hom         =   Hom {‚Ñì}
   ; mor         =   mor
   ; comp        =   Œª F G ‚Üí record
     { mor       =   mor F ‚àò mor G
     ; pres-Id   =   ‚â°.cong (mor F) (pres-Id G) ‚ü®‚â°‚â°‚ü© pres-Id F
     ; pres-Op   =   ‚â°.cong (mor F) (pres-Op G) ‚ü®‚â°‚â°‚ü© pres-Op F
     }
   ; comp-is-‚àò   =   ‚âê-refl
   ; Id          =   MkHom id ‚â°.refl ‚â°.refl
   ; Id-is-id    =   ‚âê-refl
   }

MonoidCat : (‚Ñì : Level) ‚Üí Category (lsuc ‚Ñì) ‚Ñì ‚Ñì
MonoidCat ‚Ñì = oneSortedCategory ‚Ñì MonoidAlg
\end{code}
%}}}

%{{{ forgetful functor
\subsection{Forgetful Functor (to Sets)}
Forget all structure, and maintain only the underlying carrier

\begin{code}
Forget : (‚Ñì : Level) ‚Üí Functor (MonoidCat ‚Ñì) (Sets ‚Ñì)
Forget ‚Ñì = record
  { F‚ÇÄ = Carrier
  ; F‚ÇÅ = mor
  ; identity = ‚â°.refl
  ; homomorphism = ‚â°.refl
  ; F-resp-‚â° = _$·µ¢
  }

Forget-alg : (‚Ñì : Level) ‚Üí Functor (MonoidCat ‚Ñì) (Sets ‚Ñì)
Forget-alg ‚Ñì = mkForgetful ‚Ñì MonoidAlg
\end{code}
%}}}

%{{{ Useful kit
\begin{code}
ind : {‚Ñì ‚Ñì‚Ä≤ : Level} {Y : Set ‚Ñì} (P : List Y ‚Üí Set ‚Ñì‚Ä≤)
    ‚Üí (P [])
    ‚Üí ((y : Y) (ys : List Y) ‚Üí P ys ‚Üí P (y ‚à∑ ys))
    ‚Üí (ys : List Y) ‚Üí P ys
ind _ n _ []         =   n
ind P n c (x ‚à∑ xs)   =   c x xs (ind P n c xs)
\end{code}
}}}%

%{{{ Free functor; ListLeft
\begin{code}
Free : (‚Ñì : Level) ‚Üí Functor (Sets ‚Ñì) (MonoidCat ‚Ñì)
Free ‚Ñì = record
  { F‚ÇÄ = Œª a ‚Üí record
    { Carrier = List a
    ; Id = []
    ; _*_ = _++_
    ; leftId = ‚â°.refl
    ; rightId = Œª {x} ‚Üí ++-identity ≥ x
    ; assoc = Œª {x y z} ‚Üí ++-assoc x y z
    }
  ; F‚ÇÅ = Œª f ‚Üí MkHom (map f) ‚â°.refl Œª {xs} {ys} ‚Üí map-++-commute f xs ys
  ; identity = map-id
  ; homomorphism = map-compose
  ; F-resp-‚â° = Œª F‚âêG ‚Üí map-cong Œª x ‚Üí F‚âêG {x}
  }

ListLeft : (‚Ñì : Level) ‚Üí Adjunction (Free ‚Ñì) (Forget ‚Ñì)
ListLeft ‚Ñì = record
  { unit = record { Œ∑ = Œª _ x ‚Üí [ x ]
                  ; commute = Œª _ ‚Üí ‚â°.refl }
  ; counit = record { Œ∑ = Œª X ‚Üí
    let fold = foldr (_*_ X) (Id X)
        _+_ = _*_ X
        e   = Id X in
    MkHom fold ‚â°.refl
          Œª {x} {y} ‚Üí ind (Œª l ‚Üí fold (l ++ y) ‚â° fold l + fold y)
                          (‚â°.sym (leftId X))
                          (Œª z zs eq ‚Üí ‚â°.trans (‚â°.cong (z +_) eq) (‚â°.sym (assoc X))) x
                    ; commute = Œª {X} {Y} f l ‚Üí
   let foldX = foldr (_*_ X) (Id X)
       foldY = foldr (_*_ Y) (Id Y)
       _+_ = _*_ Y in
       ind (Œª ll ‚Üí foldY (map (mor f) ll) ‚â° mor f (foldX ll))
           (‚â°.sym (pres-Id f))
           (Œª z zs eq ‚Üí ‚â°.trans (‚â°.cong ((mor f z) +_) eq) (‚â°.sym (pres-Op f)) ) l }
  ; zig = Œª l ‚Üí ind (Œª ll ‚Üí ll ‚â° foldr _++_ [] (map [_] ll)) ‚â°.refl (Œª y ys eq ‚Üí ‚â°.cong (y ‚à∑_) eq) l
  ; zag = Œª {X} ‚Üí ‚â°.sym (rightId X)
  }
\end{code}
%}}}

%{{{ 0-Ary version
\begin{code}
module ZeroAryAdjoint where

  open import Structures.OneCat

  Forget-0 : (‚Ñì : Level) ‚Üí Functor (MonoidCat ‚Ñì) (OneCat ‚Ñì ‚Ñì ‚Ñì)
  Forget-0 ‚Ñì = record
    { F‚ÇÄ = Carrier
    ; F‚ÇÅ = Œª _ ‚Üí ‚ãÜ
    ; identity = ‚ãÜ
    ; homomorphism = ‚ãÜ
    ; F-resp-‚â° = Œª _ ‚Üí ‚ãÜ
    }

  -- OneCat can be, itself, viewed as a Monoid
  Free-0 : (‚Ñì : Level) ‚Üí Functor (OneCat ‚Ñì ‚Ñì ‚Ñì) (MonoidCat ‚Ñì)
  Free-0 ‚Ñì = record
     { F‚ÇÄ             =  Œª _ ‚Üí record
                                 { Carrier = One
                                 ; Id = ‚ãÜ
                                 ; _*_ = ùë≤‚ÇÇ ‚ãÜ
                                 ; leftId = Œª { {‚ãÜ} ‚Üí ‚â°.refl}
                                 ; rightId = Œª { {‚ãÜ} ‚Üí ‚â°.refl}
                                 ; assoc = ‚â°.refl
                                 }
     ; F‚ÇÅ             =  Œª _ ‚Üí MkHom id ‚â°.refl ‚â°.refl
     ; identity       =  Œª _ ‚Üí ‚â°.refl
     ; homomorphism   =  Œª _ ‚Üí ‚â°.refl
     ; F-resp-‚â°      =   Œª _ _ ‚Üí ‚â°.refl
     }

  Left : (‚Ñì : Level) ‚Üí Adjunction (Free-0 ‚Ñì) (Forget-0 ‚Ñì)
  Left ‚Ñì = record
    { unit        =   record { Œ∑ = Œª _ ‚Üí ‚ãÜ ; commute = id }
    ; counit      =   record
      { Œ∑         =   Œª X ‚Üí MkHom (Œª _ ‚Üí Id X) ‚â°.refl (‚â°.sym (leftId X {Id X}))
      ; commute   =    Œª f x ‚Üí ‚â°.sym (pres-Id f)
      }
    ; zig         =    Œª { ‚ãÜ ‚Üí ‚â°.refl}
    ; zag         =    ‚ãÜ
    }
\end{code}
%}}}
-- ToDo ‚à∑ forget to the underlying semigroup

-- ToDo ‚à∑ forget to the underlying pointed

-- ToDo ‚à∑ forget to the underlying magma

-- ToDo ‚à∑ forget to the underlying binary relation, with |x ‚àº y ‚à∂‚â° (‚àÄ z ‚Üí x * z ‚â° y * z)|
          -- the monoid-indistuighability equivalence relation


% Quick Folding Instructions:
% C-c C-s :: show/unfold region
% C-c C-h :: hide/fold region
% C-c C-w :: whole file fold
% C-c C-o :: whole file unfold
%
% Local Variables:
% folded-file: t
% eval: (fold-set-marks "%{{{ " "%}}}")
% eval: (fold-whole-buffer)
% fold-internal-margins: 0
% end:
