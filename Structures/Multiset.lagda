\section{Structures.Multiset}

%{{{ Imports
\begin{code}
module Structures.Multiset where

open import Level renaming (zero to lzero; suc to lsuc ; _âŠ”_ to _âŠ_) hiding (lift)
open import Relation.Binary using (Setoid; Rel; IsEquivalence)

-- open import Categories.Category   using (Category)
open import Categories.Functor    using (Functor)
open import Categories.Adjunction using (Adjunction)
open import Categories.Agda       using (Setoids)

open import Function.Equality using (Î  ; _âŸ¶_ ; id ; _âˆ˜_)

open import Data.List     using (List; []; _++_; _âˆ·_; foldr)  renaming (map to mapL)
open import Data.List.Properties using (map-++-commute; map-id; map-compose)

open import DataProperties hiding (âŸ¨_,_âŸ©)
open import SetoidEquiv
open import ParComp
open import EqualityCombinators
open import Belongs
open import Structures.CommMonoid renaming (Hom to CMArrow)

open import Algebra   using (Monoid)
open import Data.List using (monoid)
\end{code}
%}}}

%{{{ CtrSetoid
\subsection{CtrSetoid}

As will be explained below, the kind of ``container'' ---|ctr|--- used for
building a |Multiset| needs to support a |Setoid|-polymorphic
equivalence relation.
\begin{code}
record IsCtrEquivalence {â„“ : Level} (o : Level) (Ctr : Set â„“ â†’ Set â„“)
  : Set (lsuc â„“ âŠ lsuc o) where
  field
    equiv        : (X : Setoid â„“ (o âŠ â„“)) â†’ Rel (Ctr (Setoid.Carrier X)) (o âŠ â„“)
    equivIsEquiv : (X : Setoid â„“ (o âŠ â„“)) â†’ IsEquivalence (equiv X)
  -- handy dandy syntactic sugar for |k|ontainer equality
  infix -666 equiv
  syntax equiv X s t  =  s â‰ˆâ‚– t âˆ¶ X   -- ghost colon
\end{code}

We have a type transformer |ctr| that furnishes setoids with an equivalence relation |equiv|.

\edcomm{MA}{Since there are no `coherencey' constraints, we might as well say that this
|IsCtrEquivalence| is nothing more than a setoid transformer: The object component of an endofunctor
on the category of setoids. Indeed:}

\begin{code}
  ctrSetoid : (X : Setoid â„“ (o âŠ â„“)) â†’ Setoid â„“ (â„“ âŠ o)
  ctrSetoid X = record
    { Carrier        =  Ctr (Setoid.Carrier X)
    ; _â‰ˆ_            =  equiv X
    ; isEquivalence  =  equivIsEquiv X
    }
\end{code}
%}}}

%{{{ CommutativeContainer

In the same vein as before, we consider a setoid-polymorphic equivalence relation that
also furnishes a raw type with a commutative monoid structure. That is, we know have
a the object-component of a functor from the category of setoids to the category of
commutative monoids.

\begin{code}
record CommutativeContainer (â„“ c : Level) : Set (lsuc â„“ âŠ lsuc c) where
  open IsCtrEquivalence using (equiv)
  field
    ğ’                    :   Set â„“ â†’ Set â„“
    isCtrEquivalence     :   IsCtrEquivalence c ğ’
    âˆ…                    :  {X : Set â„“} â†’ ğ’ X
    _âŠ•_                  :  {X : Set â„“} â†’ ğ’ X â†’ ğ’ X â†’ ğ’ X
    isCommutativeMonoid  :  {X : Setoid â„“ (c âŠ â„“)} â†’ IsCommutativeMonoid (equiv isCtrEquivalence X) _âŠ•_ âˆ…

  open IsCtrEquivalence isCtrEquivalence             public

  commMonoid : (X : Setoid â„“ (c âŠ â„“)) â†’ CommMonoid (ctrSetoid X)
  commMonoid X = record
    { e              =   âˆ…
    ; _*_            =   _âŠ•_
    ; isCommMonoid   =   isCommutativeMonoid
    }
\end{code}

%}}}

%{{{ Multiset
\subsection{Multiset}
A â€œmultiset on type Xâ€ is a structure on which one can define
\begin{itemize}
\item a \emph{commutative monoid} structure,
\item implement the concept of \emph{singleton}
\item implement the concept of \emph{fold}; note that the name
is inspired by its implementation in the main model.  Its signature
would have suggested ``extract'', but this would have been
quite misleading.
\end{itemize}

\begin{code}
open Î           using () renaming (_âŸ¨$âŸ©_ to _âŸ¨$âŸ©â‚€_)
open CMArrow    using (_âŸ¨$âŸ©_ ; mor ; pres-e ; pres-*)
open CommMonoid using (eq-in ; isCommMonoid)

record Multiset {â„“ c : Level} (X : Setoid â„“ (c âŠ â„“)) : Set (lsuc â„“ âŠ lsuc c) where  
  field
    commutativeContainer : CommutativeContainer â„“ c

  open CommutativeContainer commutativeContainer     public
  open Setoid X using (_â‰ˆ_) renaming (Carrier to Xâ‚€)  

  field
    singleton       :  X âŸ¶ ctrSetoid X
    fold            :  {Y : Setoid â„“ (c âŠ â„“)} (CMY : CommMonoid Y) â†’ CMArrow (commMonoid Y) CMY
    fold-singleton  :  {CM : CommMonoid X} (x : Xâ‚€) â†’ x â‰ˆ fold CM âŸ¨$âŸ© (singleton âŸ¨$âŸ©â‚€ x)
\end{code}

A â€œmultiset homomorphismâ€ is a way to lift arbitrary (setoid) functions on the carriers
to be homomorphisms on the underlying commutative monoid structure, as well as a few
compatibility laws.

In the classical contexts of sets and set-functions, the constraints take the form:
|{ f x } â‰ˆ lift f { x }| and |fold (lift f s) â‰ˆ f (fold s)|. In particular, the |lift| operation
mimics the behaviour of the morphism, or â€œmapâ€, portion of a functor.

\begin{code}
record MultisetHom {â„“ c : Level} {X Y : Setoid â„“ (c âŠ â„“)} (A : Multiset X) (B : Multiset Y)
  : Set (lsuc â„“ âŠ lsuc c) where
  open Multiset {â„“} {c}
  Xâ‚€ = Setoid.Carrier X
  open Setoid Y using (_â‰ˆ_)

  private
    ğ“œ = Î» {Z : Setoid â„“ (c âŠ â„“)} (CMZ : Multiset Z) â†’ commMonoid CMZ Z
    ğ’®  = Î» {Z : Setoid â„“ (c âŠ â„“)} (CMZ : Multiset Z) â†’ singleton CMZ âŸ¨$âŸ©â‚€_

  field
    lift : (X âŸ¶ Y) â†’ CMArrow (ğ“œ A) (ğ“œ B)

    -- This ensures that |singleton| is sufficiently polymorphic; i.e., a natural transformation.
    -- See the Adjunction below.
    singleton-commute : (F : X âŸ¶ Y) {x : Xâ‚€} â†’ ğ’® B (F âŸ¨$âŸ©â‚€ x) â‰ˆ lift F âŸ¨$âŸ© (ğ’® A x)  âˆ¶  ğ“œ B

    fold-commute : {CMX : CommMonoid X} {CMY : CommMonoid Y} (F : CMArrow CMX CMY)
                 â†’ {s : ğ’ A Xâ‚€}
                 â†’ fold B CMY âŸ¨$âŸ© (lift (mor F) âŸ¨$âŸ© s)  â‰ˆ  F âŸ¨$âŸ© (fold A CMX âŸ¨$âŸ© s)
                 
open MultisetHom
\end{code}

And now something somewhat different: to express that we have the right
functoriality properties (and ``zap''), we need to assume that we have
\emph{constructors} of |Multiset| and |MultisetHom|.  With these in hand,
we can then phrase what extra properties must hold.  Because these properties
hold at ``different types'' than the ones for the underlying ones, these
cannot go into the above.
\begin{code}
record FunctorialMSH {â„“ c : Level} (MS : (X : Setoid â„“ (c âŠ â„“)) â†’ Multiset X)
    (MSH : {X Y : Setoid â„“ (c âŠ â„“)} â†’ MultisetHom {â„“} {c} {X} {Y} (MS X) (MS Y))
    : Set (lsuc â„“ âŠ lsuc c) where
  open Multiset
  open MultisetHom
  open Setoid   using (Carrier)
  open IsCtrEquivalence hiding (ctrSetoid)
  private
    Obj = Setoid â„“ (c âŠ â„“)
    ğ’â‚˜ = Î» X â†’ ğ’ (MS X) (Carrier X)
    ğ“œ = Î» X â†’ commMonoid (MS X) X
    ğ‘³  = Î» {X Y : Obj}  (F   : X âŸ¶ Y) â†’ lift MSH F
    _â‰‹_ = Î» {X : Obj} (l r : ğ’â‚˜ X) â†’ l â‰ˆ r âˆ¶ ğ“œ X
  
  field
    -- Lifting the identity yields an identity morphism.
    id-pres : {X : Obj} {x : ğ’â‚˜ X} â†’ ğ‘³ id âŸ¨$âŸ© x  â‰ˆ  x  âˆ¶  ğ“œ X

    -- Lifting preserves composition.
    âˆ˜-pres : {X Y Z : Obj} {F : X âŸ¶ Y} {G : Y âŸ¶ Z}
           â†’ {x : ğ’â‚˜ X} â†’ (ğ‘³ (G âˆ˜ F)) âŸ¨$âŸ© x â‰ˆ ğ‘³ G âŸ¨$âŸ© (ğ‘³ F âŸ¨$âŸ© x)  âˆ¶  ğ“œ Z

    -- Lifting preserves extensional equality.
    resp-â‰ˆ : {X Y : Obj} {F G : X âŸ¶ Y} (let open Setoid Y renaming (_â‰ˆ_ to _â‰ˆâ‚€_))
          â†’ (Fâ‰ˆG : {x : Carrier X} â†’ F âŸ¨$âŸ©â‚€ x â‰ˆâ‚€ G âŸ¨$âŸ©â‚€ x)
          â†’ {x : ğ’â‚˜ X} â†’ ğ‘³ F âŸ¨$âŸ© x â‰ˆ ğ‘³ G âŸ¨$âŸ© x  âˆ¶  ğ“œ Y

    -- Lifting the singleton mapping then folding yields the orginal result.
    -- In particular, the singleton construction is injective --as we'd like.
    fold-lift-singleton : {X : Obj} (let ms = MS X ; _â‰ˆ_ = equiv (isCtrEquivalence ms) X)
      â†’ {s : ğ’â‚˜ X} â†’ s â‰ˆ (fold (MS (ctrSetoid ms X)) (ğ“œ X) âŸ¨$âŸ© (ğ‘³ (singleton ms) âŸ¨$âŸ© s))
\end{code}
%}}}

%{{{ BuildLeftAdjoint
Given an implementation of a |Multiset| as well as of |MultisetHom| over that,
build a Free Functor which is left adjoint to the forgetful functor.

\begin{code}
module BuildLeftAdjoint
  (MS   : {â„“ c : Level} (X : Setoid â„“ (â„“ âŠ c)) â†’ Multiset X)
  (MSH  : {â„“ c : Level} {X Y : Setoid â„“ (â„“ âŠ c)} â†’ MultisetHom {â„“} {c} (MS X) (MS {c = c} Y))
  (Func : {â„“ c : Level} â†’ FunctorialMSH {â„“} {c} MS MSH )
  where

  open Multiset
  open MultisetHom
  open FunctorialMSH

  Free : (â„“ c : Level) â†’ Functor (Setoids â„“ (â„“ âŠ c)) (MonoidCat â„“ (â„“ âŠ c))
  Free _ _ = record
    { Fâ‚€             =   Î» S â†’ ctrSetoid (MS S) S , commMonoid (MS S) S
    ; Fâ‚             =   Î» F â†’ record { CMArrow (lift MSH F) }
    ; identity       =   id-pres Func
    ; homomorphism   =   âˆ˜-pres Func
    ; F-resp-â‰¡       =   resp-â‰ˆ Func
    }

  LeftAdjoint : {â„“ o : Level} â†’ Adjunction (Free â„“ o) (Forget â„“ (â„“ âŠ o))
  LeftAdjoint = record
    { unit = record
      { Î· = Î» X â†’ singleton (MS X)
      ; commute = singleton-commute MSH
      }
    ; counit = record
      { Î·        =  Î» { (X , cm) â†’ record { CMArrow (fold (MS X) cm) } }
      ; commute  =  fold-commute MSH
      }
    ; zig = fold-lift-singleton Func
    ; zag = Î» { {X , CM} {m} â†’ fold-singleton (MS X) m }
    }
\end{code}
%}}}

%{{{ An implementation of |Multiset| using lists with Bag equality
\subsection{An implementation of |Multiset| using lists with Bag equality}
\begin{code}
module ImplementationViaList {â„“ o : Level} (X : Setoid â„“ (â„“ âŠ o)) where
  open Setoid  
  open ElemOfSing X
\end{code}

\begin{code}
  ListMS : Multiset {â„“} {o} X
  ListMS = record
    { commutativeContainer = record
         { ğ’                   = List
         ; isCtrEquivalence    = record
            { equiv        = Î» Y â†’ let open BagEq Y in _â‡”_
            ; equivIsEquiv = Î» _ â†’ record { refl = â‰…-refl ; sym = â‰…-sym ; trans = â‰…-trans }
            }
         ; âˆ…                   = []
         ; _âŠ•_                 = _++_
         ; isCommutativeMonoid = Î» {Y} â†’
           let
             open BagEq       Y   using   (â‰¡â†’â‡”)
             open Membership  Y   using   (elem-of)
             open ConcatToâŠS  Y   using   (âŠSâ‰…++)
             module ++ = Monoid (monoid (Carrier Y))
           in record
            { left-unit  = Î» y â†’ â‰…-refl
            ; right-unit = Î» ys â†’ â‰¡â†’â‡” (projâ‚‚ ++.identity ys)
            ; assoc      = Î» xs ys zs â†’ â‰¡â†’â‡” (++.assoc xs ys zs)
            ; comm       = Î» xs ys â†’ 
               elem-of (xs ++ ys)         â‰…Ë˜âŸ¨ âŠSâ‰…++ âŸ©
               elem-of xs âŠS elem-of ys   â‰…âŸ¨ âŠS-comm _ _ âŸ©
               elem-of ys âŠS elem-of xs   â‰…âŸ¨ âŠSâ‰…++ âŸ©
               elem-of (ys ++ xs)         âˆ
            ; _âŸ¨âˆ™âŸ©_      = Î» {x} {y} {z} {w} xâ‡”y zâ‡”w â†’
               elem-of (x ++ z)          â‰…Ë˜âŸ¨ âŠSâ‰…++ âŸ©
               elem-of x âŠS elem-of z    â‰…âŸ¨ xâ‡”y âŠSâ‚ zâ‡”w âŸ©
               elem-of y âŠS elem-of w    â‰…âŸ¨ âŠSâ‰…++ âŸ©
               elem-of (y ++ w)          âˆ
            }
         }
    ; singleton = record { _âŸ¨$âŸ©_ = Î» x â†’ x âˆ· [] ; cong = singleton-â‰ˆ }
    ; fold = Î» {Y} CMX â†’ let open CommMonoid CMX in record
         { mor      =   record { _âŸ¨$âŸ©_ = foldr _*_ e ; cong = fold-permute {CM = CMX} }
         ; pres-e   =   refl Y
         ; pres-*   =   fold-CM-over-++ CMX
         }
    ; fold-singleton = Î» {CM} x â†’ sym X (right-unit (isCommMonoid CM) x)
    }
    where
       open IsCommutativeMonoid using (left-unit ; right-unit ; assoc) renaming (_âŸ¨âˆ™âŸ©_ to cong)
       open import Relation.Binary.SetoidReasoning renaming (_âˆ to _â– )
        
       fold-CM-over-++ : {Z : Setoid â„“ (o âŠ â„“)} (cm : CommMonoid Z) {s t : List (Carrier Z)}
                       â†’  let open CommMonoid cm ; F = foldr _*_ e in
                           F (s ++ t) â‰ˆâŒŠ Z âŒ‹ (F s * F t)
       fold-CM-over-++ {Z} (MkCommMon e _*_ isCommMon) {[]} {t} = sym Z (left-unit isCommMon _)
       fold-CM-over-++ {Z} CMZ@(MkCommMon e _*_ isCommMon) {x âˆ· s} {t} = beginâŸ¨ Z âŸ©
         let F = foldr _*_ e in
         x * F (s ++ t)  â‰ˆâŸ¨ cong isCommMon (refl Z) (fold-CM-over-++ CMZ ) âŸ©
         x * (F s * F t) â‰ˆâŸ¨ sym Z (assoc isCommMon _ _ _)                  âŸ©
         (x * F s) * F t â– 
       
       fold-permute : {Z : Setoid â„“ (o âŠ â„“)} {CM : CommMonoid Z} {s t : List (Carrier Z)}
                      (let open BagEq Z ; open CommMonoid CM)
                    â†’ s â‡” t
                    â†’ foldr _*_ e s â‰ˆâŒŠ Z âŒ‹ foldr _*_ e t
       fold-permute {Z} {MkCommMon e _*_ isCommMon} {s} {t} pf = {!!}
\end{code}

\begin{spec}
      open Locations
      open Membership using (El)
      open ElemOf[]
      fold-permute : {Z : Setoid â„“ o} {CM : CommMonoid Z} {i j : List (Setoid.Carrier Z)} â†’
        let open BagEq Z in let open CommMonoid CM renaming (_*_ to _+_; e to eâ‚) in
        i â‡” j â†’ foldr _+_ eâ‚ i â‰ˆâŒŠ Z âŒ‹ foldr _+_ eâ‚ j
      fold-permute {Z} {CM} {[]} {[]} iâ‡”j = Setoid.refl Z
      fold-permute {Z} {CM} {[]} {x âˆ· j} iâ‡”j =
        âŠ¥-elim (elem-of-[] Z (_â‰…_.from iâ‡”j Î .âŸ¨$âŸ© El (here (Setoid.refl Z))))
      fold-permute {Z} {CM} {x âˆ· i} {[]} iâ‡”j =
        âŠ¥-elim (elem-of-[] Z (_â‰…_.to iâ‡”j Î .âŸ¨$âŸ© El (here (Setoid.refl Z))))
      fold-permute {Z} {CM} {x âˆ· i} {xâ‚ âˆ· j} iâ‡”j = {!!}

ListCMHom : âˆ€ {â„“ o} (X Y : Setoid â„“ (â„“ âŠ o))
  â†’ MultisetHom {o = o} (ImplementationViaList.ListMS X) (ImplementationViaList.ListMS Y)
ListCMHom {â„“} {o} X Y = record
  { lift = Î» F â†’ let g = Î ._âŸ¨$âŸ©_ F in record
    { mor = record
      { _âŸ¨$âŸ©_ = mapL g
      ; cong = Î» {xs} {ys} xsâ‰ˆys â†’
        elem-of (mapL g xs)   â‰…âŸ¨ shift-map F xs âŸ©
        shifted F xs          â‰…âŸ¨ shifted-cong F xsâ‰ˆys âŸ©
        shifted F ys          â‰…Ë˜âŸ¨ shift-map F ys âŸ©
        elem-of (mapL g ys) âˆ
      }
    ; pres-e =
         elem-of []     â‰…Ë˜âŸ¨ âŠ¥âŠ¥â‰…elem-of-[] âŸ©
         âŠ¥âŠ¥             â‰…âŸ¨ âŠ¥âŠ¥â‰…elem-of-[] âŸ©
         (elem-of eâ‚) âˆ

      -- in the proof below, *â‚€ and *â‚ are both ++
    ; pres-* = Î» {x} {y} â†’
      elem-of (mapL g (x *â‚€ y))           â‰…âŸ¨ â‰¡â†’â‡” (map-++-commute g x y) âŸ©
      elem-of (mapL g x *â‚ mapL g y) âˆ
    }
  ; singleton-commute = Î» f {x} â†’ â‰…-refl
  ; fold-commute = f-comm
  }
    where
      open ImplementationViaList
      open CommMonoid (Multiset.commMonoid (ListMS X)) renaming (e to eâ‚€; _*_ to _*â‚€_)
      open CommMonoid (Multiset.commMonoid (ListMS Y)) renaming (e to eâ‚; _*_ to _*â‚_)
      open Membership Y using (elem-of)
      open BagEq Y using (â‰¡â†’â‡”)
      open ElemOfMap
      open ElemOf[] Y
      f-comm : {W : CommMonoid X} {Z : CommMonoid Y} (f : Hom (X , W) (Y , Z))
        {lx : List (Setoid.Carrier X)} â†’
        Setoid._â‰ˆ_ Y (foldr (CommMonoid._*_ Z) (CommMonoid.e Z) (mapL (Î ._âŸ¨$âŸ©_ (Hom.mor f)) lx))
                     (Hom.mor f Î .âŸ¨$âŸ© foldr (CommMonoid._*_ W) (CommMonoid.e W) lx)
      f-comm {MkCommMon e _*_ isCommMonoidâ‚} {MkCommMon eâ‚‚ _*â‚‚_ isCMâ‚‚} f {[]} =
        Setoid.sym Y (Hom.pres-e f)
      f-comm {MkCommMon e _*_ isCommMonoidâ‚} {MkCommMon eâ‚‚ _*â‚‚_ isCMâ‚‚} f {x âˆ· lx} =
        let g = Î ._âŸ¨$âŸ©_ (Hom.mor f) in  beginâŸ¨ Y âŸ©
         ((g x) *â‚‚ (foldr _*â‚‚_ eâ‚‚ (mapL g lx)))  â‰ˆâŸ¨ refl âŸ¨âˆ™âŸ© f-comm f {lx} âŸ©
         ((g x) *â‚‚ (g (foldr _*_ e lx)))         â‰ˆâŸ¨ sym (Hom.pres-* f) âŸ©
         (g (x * foldr _*_ e lx)) â– 
        where
          open Setoid Y
          open import Relation.Binary.SetoidReasoning using (_â‰ˆâŸ¨_âŸ©_; beginâŸ¨_âŸ©_) renaming (_âˆ to _â– )
          open IsCommutativeMonoid isCMâ‚‚ using (_âŸ¨âˆ™âŸ©_)

module BuildProperties where
  open ImplementationViaList
  functoriality : {â„“ o : Level} â†’ FunctorialMSH {â„“} {o} ListMS ListCMHom
  functoriality {â„“} {o} = record
    { id-pres = Î» {X} {x} â†’ BagEq.â‰¡â†’â‡” X (map-id x)
    ; âˆ˜-pres = Î» {_} {_} {Z} {f} {g} {x} â†’ BagEq.â‰¡â†’â‡” Z (map-compose x)
    ; resp-â‰ˆ = Î» {A} {B} {F} {G} Fâ‰ˆG {l} â†’ respect-â‰ˆ {F = F} {G} Fâ‰ˆG l
    ; fold-lift-singleton = Î» {X} {l} â†’ BagEq.â‰¡â†’â‡” X (concat-singleton l)
    }
    where
    open Membership
    open Locations using (here; there)
    open Setoid using (Carrier; trans; sym)
    open Multiset using (Ctr; commMonoid)
    respect-â‰ˆ : {A B : Setoid â„“ (o âŠ â„“)} {F G : A âŸ¶ B}
      (Fâ‰ˆG : {x : Carrier A} â†’ F Î .âŸ¨$âŸ© x â‰ˆâŒŠ B âŒ‹ G Î .âŸ¨$âŸ© x)
      (lst : Ctr (ListMS A) (Carrier A))
      â†’ mapL (Î ._âŸ¨$âŸ©_ F) lst â‰ˆ mapL (Î ._âŸ¨$âŸ©_ G) lst âˆ¶ commMonoid (ListMS B)
    respect-â‰ˆ                 Fâ‰ˆG [] = â‰…-refl
    respect-â‰ˆ {A} {B} {F} {G} Fâ‰ˆG (x âˆ· lst) = record
      { to = record { _âŸ¨$âŸ©_ = to-G ; cong = cong-to-G }
      ; from = record { _âŸ¨$âŸ©_ = from-G ; cong = cong-from-G }
      ; inverse-of = record { left-inverse-of = left-inv ; right-inverse-of = right-inv } }
        where
          open LocEquiv B
          f = mapL (Î ._âŸ¨$âŸ©_ F)
          g = mapL (Î ._âŸ¨$âŸ©_ G)

          to-G : {l : List (Carrier A)} â†’ elements B (f l) â†’ elements B (g l)
          to-G {[]} (El ())
          to-G {_ âˆ· _} (El (here sm)) = El (here (trans B sm Fâ‰ˆG))
          to-G {_ âˆ· _} (El (there belongs)) = lift-el B there (to-G (El belongs))

          cong-to-G : {l : List (Carrier A)} {i j : elements B (f l)} â†’ belongs i â‰‹ belongs j
            â†’ belongs (to-G i) â‰‹ belongs (to-G j)
          cong-to-G {[]} ()
          cong-to-G {_ âˆ· _} (hereEq xâ‰ˆz yâ‰ˆz) = LocEquiv.hereEq (trans B xâ‰ˆz Fâ‰ˆG) (trans B yâ‰ˆz Fâ‰ˆG)
          cong-to-G {_ âˆ· _} (thereEq iâ‰‹j) = LocEquiv.thereEq (cong-to-G iâ‰‹j)

          from-G : {l : List (Carrier A)} â†’ elements B (g l) â†’ elements B (f l)
          from-G {[]} (El ())
          from-G {_ âˆ· _} (El (here sm)) = El (here (trans B sm (sym B Fâ‰ˆG)))
          from-G {_ âˆ· xs} (El (there xâ‚)) = lift-el B there (from-G (El xâ‚))

          cong-from-G : {l : List (Carrier A)} {i j : elements B (g l)} â†’ belongs i â‰‹ belongs j
            â†’ belongs (from-G i) â‰‹ belongs (from-G j)
          cong-from-G {[]} ()
          cong-from-G {_ âˆ· _} (hereEq xâ‰ˆz yâ‰ˆz) = hereEq (trans B xâ‰ˆz (sym B Fâ‰ˆG)) (trans B yâ‰ˆz (sym B Fâ‰ˆG))
          cong-from-G {_ âˆ· _} (thereEq locâ‚) = thereEq (cong-from-G locâ‚)

          left-inv : {l : List (Carrier A)} (y : elements B (mapL (Î ._âŸ¨$âŸ©_ F) l))
            â†’ belongs (from-G (to-G y)) â‰‹ belongs y
          left-inv {[]} (El ())
          left-inv {_ âˆ· _} (El (here sm)) = hereEq (trans B (trans B sm Fâ‰ˆG) (sym B Fâ‰ˆG)) sm
          left-inv {_ âˆ· _} (El (there belongsâ‚)) = thereEq (left-inv (El belongsâ‚))

          right-inv : {l : List (Carrier A)} (y : elements B (mapL (Î ._âŸ¨$âŸ©_ G) l))
            â†’ belongs (to-G (from-G y)) â‰‹ belongs y
          right-inv {[]} (El ())
          right-inv {_ âˆ· _} (El (here sm)) = hereEq (trans B (trans B sm (sym B Fâ‰ˆG)) Fâ‰ˆG) sm
          right-inv {_ âˆ· _} (El (there belongsâ‚)) = thereEq (right-inv (El belongsâ‚))
    concat-singleton : {X : Set â„“} (lst : List X)
      â†’ lst â‰¡ foldr _++_ [] (mapL (Î» x â†’ x âˆ· []) lst)
    concat-singleton [] = â‰¡.refl
    concat-singleton (x âˆ· lst) = â‰¡.cong (Î» z â†’ x âˆ· z) (concat-singleton lst)
\end{spec}

Last but not least, build the left adjoint:

\begin{spec}
module FreeCommMonoid = BuildLeftAdjoint ImplementationViaList.ListMS ListCMHom
  BuildProperties.functoriality
\end{spec}
%}}}

% Quick Folding Instructions:
% C-c C-s :: show/unfold region
% C-c C-h :: hide/fold region
% C-c C-w :: whole file fold
% C-c C-o :: whole file unfold
%
% Local Variables:
% folded-file: t
% eval: (fold-set-marks "%{{{ " "%}}}")
% eval: (fold-whole-buffer)
% fold-internal-margins: 0
% end:
