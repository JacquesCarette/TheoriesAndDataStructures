\begin{code}
{-# OPTIONS --irrelevant-projections --allow-unsolved-metas #-}
\end{code}

\section{UnaryAlgebra}

Unary algebras are tantamount to an OOP interface with a single operation.
The associated free structure captures the ``syntax'' of such interfaces, say, for the sake
of delayed evaluation in a particular interface implementation.

This example algebra serves to set-up the approach we take in more involved settings.

\edcomm{MA}{This section requires massive reorganisation.}

%{{{ Imports
\begin{code}
module Structures.UnaryAlgebra where

open import Level renaming (suc to lsuc; zero to lzero)
open import Function
open import Relation.Nullary using (Â¬_)
open import Data.Nat using (â„•; suc ; zero)

open import Helpers.Categorical
open import Helpers.Function2
open import Helpers.Forget
open import Helpers.DataProperties
open import Helpers.EqualityCombinators
\end{code}
%}}}

%{{{ \subsection{Definition} Unary ; Hom
\subsection{Definition}
A single-sorted |Unary| algebra consists of a type along with a function on that type.
For example, the naturals and addition-by-1 or lists and the reverse operation.

\begin{code}
record Unary {â„“} : Set (lsuc â„“) where
  constructor MkUnary
  field
    Carrier : Set â„“
    Op      : Carrier â†’ Carrier

open Unary

record Hom {â„“} (X Y : Unary {â„“}) : Set â„“ where
  constructor MkHom
  field
    mor        :  Carrier X â†’ Carrier Y
    pres-op    :  mor âˆ˜ Op X  â‰áµ¢  Op Y âˆ˜ mor

open Hom
\end{code}
%}}}

%{{{ \subsection{Category and Forgetful Functor} UnaryAlg ; UnaryCat ; Forget
\subsection{Category and Forgetful Functor}

Along with functions that preserve the elected operation, such algberas form a category.

\begin{code}
UnaryAlg : {â„“ : Level} â†’ OneSortedAlg â„“
UnaryAlg = record
  { Alg       = Unary
  ; Carrier   = Carrier
  ; Hom       = Hom
  ; mor       = mor
  ; comp      = Î» F G â†’ record
    { mor     =  mor F âˆ˜ mor G
    ; pres-op =  â‰¡.cong (mor F) (pres-op G) âŸ¨â‰¡â‰¡âŸ© pres-op F
    }
  ; comp-is-âˆ˜ =  â‰-refl
  ; Id        =  MkHom id â‰¡.refl
  ; Id-is-id  =  â‰-refl
  }

Unarys : (â„“ : Level) â†’ Category (lsuc â„“) â„“ â„“
Unarys â„“ = oneSortedCategory â„“ UnaryAlg

Forget : (â„“ : Level) â†’ Functor (Unarys â„“) (Sets â„“)
Forget â„“ = mkForgetful â„“ UnaryAlg
\end{code}
%}}}

%{{{ \subsection{Free Structure} Eventually ; âŸ¦_,_âŸ§ ; indE
\subsection{Free Structure}
We now turn to finding a free unary algebra.

Indeed, we do so by simply not ``interpreting'' the single function symbol that is required
as part of the definition. That is, we form the ``term algebra'' over the signature for
unary algebras.

\begin{code}
data Eventually {â„“} (A : Set â„“) : Set â„“ where
  base   :              A â†’ Eventually A
  step   :   Eventually A â†’ Eventually A
\end{code}
The elements of this type are of the form |stepâ¿ (base a)| for |a : A|.
This leads to an alternative presentation, |Eventually A   â‰…   Î£ n âˆ¶ â„• â€¢ A|
viz |stepâ¿ (base a) â†” (n , a)| ---cf |FreeÂ²| below.
Incidentally, or promisingly, |Eventually âŠ¤ â‰… â„•|.

For |(n , a)|, the tag |n| may be interpreted as â€œthe delay timeâ€ before the value |a| is obtained.
Alternatively, it can be interpreted to be the number of times that method |a| is to be executed.
Finally, these can be thought of as constant lists with value |a| of length |n| ;-)

\begin{code}
delay : âˆ€ {â„“} {A : Set â„“} â†’ â„• â†’ A â†’ Eventually A
delay zero    = base
delay (suc n) = step âˆ˜ delay n
\end{code}

We will realise this claim later on. For now, we turn to the dependent-eliminator/induction/recursion principle:
\begin{code}
elim : {â„“ a : Level} {A : Set a} {P : Eventually A â†’ Set â„“}
     â†’ ({x : A} â†’ P (base x))
     â†’ ({sofar : Eventually A} â†’ P sofar â†’ P (step sofar))
     â†’ (ev : Eventually A) â†’ P ev
elim b s (base x) = b {x}
elim {P = P} b s (step e) = s {e} (elim {P = P} b s e)
\end{code}

Given an unary algebra |(B, ğ’·, ğ“ˆ)| we can interpret the terms of |Eventually A|
where the injection |base| is reified by |ğ’·| and the unary operation |step| is
reified by |ğ“ˆ|.

\begin{code}
open import Function using (const)
âŸ¦_,_âŸ§ : {a b : Level} {A : Set a} {B : Set b} (ğ’· : A â†’ B) (ğ“ˆ : B â†’ B) â†’ Eventually A â†’ B
âŸ¦ ğ’· , ğ“ˆ âŸ§ = elim (Î» {a} â†’ ğ’· a) ğ“ˆ
\end{code}

Notice that: The number of |ğ“ˆ|teps is preserved, |âŸ¦ ğ’· , ğ“ˆ âŸ§ âˆ˜ stepâ¿ â‰ ğ“ˆâ¿ âˆ˜ âŸ¦ ğ’· , ğ“ˆ âŸ§|.
Essentially, |âŸ¦ ğ’· , ğ“ˆ âŸ§ (stepâ¿ base x) â‰ˆ ğ“ˆâ¿ ğ’· x|. A similar general remark applies to |elim|.

\begin{code}
reflection : {a : Level} {A : Set a}  â†’  âŸ¦ base , step âŸ§  â‰  id {A = Eventually A}
reflection = elim â‰¡.refl (â‰¡.cong step)
\end{code}
%}}}

%{{{ mapeE ; âŸ¦âŸ§-naturality
Eventually is clearly a functor,

\begin{code}
map : {a b : Level} {A : Set a} {B : Set b} â†’ (A â†’ B) â†’ (Eventually A â†’ Eventually B)
map f = âŸ¦ base âˆ˜ f , step âŸ§

map-computation : {a b : Level} {A : Set a} {B : Set b} {f : A â†’ B} {e : Eventually A}
                â†’ map f (step e) â‰¡ step (map f e)
map-computation = â‰¡.refl
\end{code}

Whence the folding operation is natural,

\begin{code}
âŸ¦âŸ§-naturality : {a b : Level} {A : Set a} {B : Set b}
              â†’ {ğ’·â€² ğ“ˆâ€² : A â†’ A} {ğ’· ğ“ˆ : B â†’ B} {f : A â†’ B}
              â†’ (basis : ğ’· âˆ˜ f â‰áµ¢ f âˆ˜ ğ’·â€²)
              â†’ (next  : ğ“ˆ âˆ˜ f â‰áµ¢ f âˆ˜ ğ“ˆâ€²)
              â†’ âŸ¦ ğ’· , ğ“ˆ âŸ§ âˆ˜ map f â‰ f âˆ˜ âŸ¦ ğ’·â€² , ğ“ˆâ€² âŸ§
âŸ¦âŸ§-naturality {ğ“ˆ = ğ“ˆ} basis next = elim basis (Î» ind â†’ â‰¡.cong ğ“ˆ ind âŸ¨â‰¡â‰¡âŸ© next)
\end{code}
%}}}

%{{{ fromE ; iterateE ; iterateE-nat

Other instances of the fold include:

\begin{code}
{- â€œforceâ€ -}
extract : âˆ€{â„“} {A : Set â„“} â†’ Eventually A â†’ A
extract = âŸ¦ id , id âŸ§ -- cf |fromâŠ| ;)
\end{code}

More generally,
\begin{code}
iterate : âˆ€ {â„“} {A : Set â„“} (f : A â†’ A) â†’ Eventually A â†’ A
iterate  f = âŸ¦ id , f âŸ§
--
-- that is, |iterateE f (stepâ¿ base x) â‰ˆ fâ¿ x|

iterate-nat : {â„“ : Level} {X Y : Unary {â„“}} (F : Hom X Y)
              â†’ iterate (Op Y) âˆ˜ map (mor F) â‰ mor F âˆ˜ iterate (Op X)
iterate-nat F = âŸ¦âŸ§-naturality {f = mor F} â‰¡.refl (â‰¡.sym (pres-op F))
\end{code}

%}}}

%{{{ iterateE-mapeE-id , mapE-id , mapE-âˆ˜ , mapE-cong

The induction rule yields identical looking proofs for clearly distinct results:

\begin{code}
iterate-map-id : {â„“ : Level} {X : Set â„“} â†’ id {A = Eventually X} â‰ iterate step âˆ˜ map base
iterate-map-id = elim â‰¡.refl (â‰¡.cong step)

map-id : {a : Level}  {A : Set a} â†’ map (id {A = A}) â‰ id
map-id = elim â‰¡.refl (â‰¡.cong step)

map-âˆ˜ : {â„“ : Level} {X Y Z : Set â„“} {f : X â†’ Y} {g : Y â†’ Z}
        â†’  map (g âˆ˜ f) â‰ map g âˆ˜ map f
map-âˆ˜ = elim â‰¡.refl (â‰¡.cong step)

map-cong : âˆ€{o} {A B : Set o} {F G : A â†’ B} â†’ F â‰ G â†’ map F â‰ map G
map-cong eq = elim (â‰¡.cong base âˆ˜ eq $áµ¢) (â‰¡.cong step)

map-congáµ¢ : âˆ€{o} {A B : Set o} {F G : A â†’ B} â†’ F â‰áµ¢ G â†’ map F â‰ map G
map-congáµ¢ eq = elim (â‰¡.cong base eq) (â‰¡.cong step)
\end{code}

These results could be generalised to |âŸ¦_,_âŸ§| if needed.

%}}}

%{{{ Free ; AdjLeft
\subsection{The Toolki Appears Naturally: Part 1}

That |Eventually| furnishes a set with its free unary algebra can now be realised.

\begin{code}
Free : (â„“ : Level) â†’ Functor (Sets â„“) (Unarys â„“)
Free â„“ = record
  { Fâ‚€             =   Î» A â†’ MkUnary (Eventually A) step
  ; Fâ‚             =   Î» f â†’ MkHom (map f) â‰¡.refl
  ; identity       =   map-id
  ; homomorphism   =   map-âˆ˜
  ; F-resp-â‰¡      =   Î» Fâ‰ˆG â†’ map-cong (Î» _ â†’ Fâ‰ˆG)
  }

AdjLeft : (â„“ : Level) â†’ Adjunction (Free â„“) (Forget â„“)
AdjLeft â„“ = record
  { unit     =   record { Î· = Î» _ â†’ base ; commute = Î» _ â†’ â‰¡.refl }
  ; counit   =   record { Î· = Î» A â†’ MkHom (iterate (Op A)) â‰¡.refl ; commute = iterate-nat }
  ; zig      =   iterate-map-id
  ; zag      =   â‰¡.refl
  }
\end{code}

Notice that the adjunction proof forces us to come-up with the operations and properties about them!
\begin{itemize}
\item |map|: usually functions can be packaged-up to work on syntax of unary algebras.
\item |map-id|: the identity function leaves syntax alone; or: |map id| can be replaced with a constant
  time algorithm, namely, |id|.
\item |map-âˆ˜|: sequential substitutions on syntax can be efficiently replaced with a single substitution.
\item |map-cong|: observably indistinguishable substitutions can be used in place of one another, similar to the
      transparency principle of Haskell programs.
\item |iterate|: given a function |f|, we have |stepâ¿ base x â†¦ fâ¿ x|. Along with properties of this operation.
\end{itemize}

%}}}

%{{{ Iteration and properties

\begin{code}
_^_ : {a : Level} {A : Set a} (f : A â†’ A) â†’ â„• â†’ (A â†’ A)
f ^ zero = id
f ^ suc n = f ^ n âˆ˜ f

-- important property of iteration that allows it to be defined in an alternative fashion
iter-swap : {â„“ : Level} {A : Set â„“} {f : A â†’ A} (n : â„•) â†’ (f ^ n) âˆ˜ f â‰ f âˆ˜ (f ^ n)
iter-swap zero = â‰-refl
iter-swap {f = f} (suc n) = âˆ˜-â‰-congâ‚ f (iter-swap n)

-- iteration of commuting functions
iter-comm : {â„“ : Level} {B C : Set â„“} {f : B â†’ C} {g : B â†’ B} {h : C â†’ C}
  â†’ (leap-frog : f âˆ˜ g â‰áµ¢ h âˆ˜ f)
  â†’ {n : â„•} â†’ h ^ n âˆ˜ f â‰áµ¢ f âˆ˜ g ^ n
iter-comm leap {zero} = â‰¡.refl
iter-comm {f = f} {g} {h} leap {suc n} {x}
  =      â‰¡.cong (h ^ n) (â‰¡.sym (leap {x}))
    âŸ¨â‰¡â‰¡âŸ© iter-comm {f = f} leap {n} {g x}

-- exponentation distributes over product
^-over-Ã— : {a b : Level} {A : Set a} {B : Set b} {f : A â†’ A} {g : B â†’ B}
         â†’ {n : â„•} â†’ (f Ã—â‚ g) ^ n â‰ (f ^ n) Ã—â‚ (g ^ n)
^-over-Ã— {n = zero} = Î»{ (x , y) â†’ â‰¡.refl}
^-over-Ã— {f = f} {g} {n = suc n} = ^-over-Ã— {n = n} âˆ˜ (f Ã—â‚ g)
\end{code}

%}}}

%{{{ Direct representation
\subsection{The Toolki Appears Naturally: Part 2}

And now for a different way of looking at the same algebra.
We ``mark'' a piece of data with its depth.

\begin{code}
FreeÂ² : (â„“ : Level) â†’ Functor (Sets â„“) (Unarys â„“)
FreeÂ² â„“ = record
  { Fâ‚€             =   Î» A â†’ MkUnary (â„• Ã— A) (suc Ã—â‚ id)
  ; Fâ‚             =   Î» f â†’ MkHom (id Ã—â‚ f) â‰¡.refl
  ; identity       =   â‰-refl
  ; homomorphism   =   â‰-refl
  ; F-resp-â‰¡      =   Î» Fâ‰ˆG â†’ Î» { (n , x) â†’ â‰¡.congâ‚‚ _,_ â‰¡.refl (Fâ‰ˆG {x}) }
  }

-- tagging operation
at : {a : Level} {A : Set a} â†’ â„• â†’ A â†’ â„• Ã— A
at n = Î» x â†’ (n , x)

ziggy : {a : Level} {A : Set a} (n : â„•) â†’ at n  â‰  (suc Ã—â‚ id {A = A}) ^ n âˆ˜ at 0
ziggy zero = â‰-refl
ziggy {A = A} (suc n) = beginâŸ¨ â‰-setoid A (â„• Ã— A) âŸ©
   (suc Ã—â‚ id)             âˆ˜ at n                            â‰ˆâŸ¨ âˆ˜-â‰-congâ‚‚ (suc Ã—â‚ id) (ziggy n) âŸ©
   (suc Ã—â‚ id)             âˆ˜ (suc Ã—â‚ id {A = A}) ^ n âˆ˜ at 0  â‰ˆâŸ¨ âˆ˜-â‰-congâ‚ (at 0) (â‰-sym (iter-swap n )) âŸ©
   (suc Ã—â‚ id {A = A}) ^ n âˆ˜ (suc Ã—â‚ id)             âˆ˜ at 0  âˆ
  where open import Relation.Binary.SetoidReasoning

AdjLeftÂ² : âˆ€ o â†’ Adjunction (FreeÂ² o) (Forget o)
AdjLeftÂ² o = record
  { unit        =   record { Î· = Î» _ â†’ at 0 ; commute = Î» _ â†’ â‰¡.refl }
  ; counit      =   record
    { Î·         =   Î» A â†’ MkHom (uncurry (Op A ^_)) (Î»{ {n , a} â†’ iter-swap n a})
    ; commute   =   Î» {X} {Y} F â†’ uncurry Î» x y â†’ iter-comm {f = mor F} {g = Op X} {h = Op Y} (pres-op F) {n = x} {y}
    }
  ; zig         =   uncurry ziggy
  ; zag         =   â‰¡.refl
  }
\end{code}

Notice that the adjunction proof forces us to come-up with the operations and properties about them!
\begin{itemize}
\item |iter-comm|: \unfinished
\item |_^_|: \unfinished
\item |iter-swap|: \unfinished
\item |ziggy|: \unfinished
\end{itemize}
%}}}

%{{{ No Right Adjoint

Suppose Forget âŠ£ Right as follows:

  f : Forget (X, op) â†’ Y
â‡” f : X â†’ Y
â‡” { magic yields a nice Op Y }
â‡” f : X â†’ Y  âˆ§  f âˆ˜ Op X â‰ˆ Op Y âˆ˜ f
â‡” f : (X, Op X) â†’ (Y, Op Y)
â‡” f : (X, Op X) â†’ Right Y

Step â€œmagicâ€ is the crux I could not pass and so is it is instructive to
select and â€œfâ€ and an â€œOp Xâ€ such that the resulting magical â€œOp Yâ€ results
in a contradiction.

It's not clear how to select these items, so let's reduce the number of choices.
For example, if we take Y = ğŸš, then (Op Y)Â³ â‰ˆ Op Y.
Now we can select f, Op X so s to obtain a contradiction:

\begin{verbatim}
  f âˆ˜ Op X â‰ˆ Op Y âˆ˜ f                  (0)
â‡’ Op Y âˆ˜ Op Y âˆ˜ f âˆ˜ Op X â‰ˆ Op Y âˆ˜ f    (leibniz)
â‡’ Op Y âˆ˜ f âˆ˜ Op X âˆ˜ Op X â‰ˆ Op Y âˆ˜ f    (0 & leibniz)
â‡’ f âˆ˜ Op X âˆ˜ Op X âˆ˜ Op X â‰ˆ Op Y âˆ˜ f    (0)
â‡’ f âˆ˜ (Op X)Â³ â‰ˆ f âˆ˜ Op X               (0)
\end{verbatim}

Neato! We found an equation that is completely independent of the magical
results ---the crucial step was that unary Boolean functions have a period of 3.
Thus if we can select an f and an Op X such that the above equation is false,
then no magical right adjoint could exist.

We can immediately dismiss two cases:
+ f cannot be the identity, otherwise X = ğŸš and (Op X)Â³ â‰ˆ Op X is provable.
+ f cannot be a constant function, since the final line would then be trivially true.
+ Op X cannot be involutionary, otherwise (Op X)Â³ â‰ˆ Op X is provable.
+ Op X cannot have period 3.

Let's try X = ğŸš Ã— ğŸš, f = projâ‚, and Op X = shuffle = Î» (a, b) â†¦ (a Ã— b, 0)
then:

\begin{verbatim}
  f âˆ˜ (Op X)Â³ â‰ˆ f âˆ˜ Op X               (above)
â‡’ projâ‚ (shuffleÂ³ (a, b)) â‰ˆ projâ‚ (shuffle (a, b)
â‡’ projâ‚ (shuffleÂ² (a Ã— b, 0)) â‰ˆ projâ‚ (a Ã— b, 0)
â‡’ projâ‚ (shuffle  (a Ã— b Ã— 0, 0)) â‰ˆ a Ã— b
â‡’ projâ‚ (0, 0) â‰ˆ a Ã— b
â‡’ 0 â‰ˆ a Ã— b
â‡’ false , provided we take a = b = 1
\end{verbatim}

This took a number of trials to find.

\begin{code}
open import Structures.OneCat hiding (Forget)
open â‰¡
open import Data.Maybe

NoRight : let â„“ = lzero in (CoFree : Functor (Sets â„“) (Unarys â„“)) â†’ Â¬ (Adjunction (Forget â„“) CoFree)
NoRight record { Fâ‚€ = Râ‚€ ; Fâ‚ = Râ‚ ; identity = identity ; homomorphism = homomorphism ; F-resp-â‰¡ = R-resp-â‰¡ } adj
  = {!!} -- âŠ¥-elim (Î· (counit adj) âŠ¥ {!mor (Î· (unit adj) (Fâ‚€ âŠ¥))!})
  where

  open Adjunction adj
  open NaturalTransformation

  open import Data.Product
  open import Data.Bool renaming (Bool to ğŸš)

  Y = ğŸš; X = ğŸš Ã— ğŸš; f = projâ‚
  shuffle : X â†’ X
  shuffle (a , b) = (a âˆ§ b , false)

  {- Suppose we have an f : X â†’ Y -}

  {- Then we find a unary object -}
  ğ’´ = Râ‚€ Y

  {- That contains a copy of Y -}
  extractâ‚‚ : Carrier ğ’´ â†’ Y
  extractâ‚‚ = Î· counit Y

  {- With its unary operation being -}
  op : Carrier ğ’´ â†’ Carrier ğ’´
  op = Op ğ’´

  {- For the source, X, we have an embedding -}

  ğ’³ = Râ‚€ X

  Î¹ : X â†’ Carrier ğ’³
  Î¹ = mor (Î· unit (MkUnary X shuffle))

  Î¹-shuffle : Î¹ âˆ˜ shuffle  â‰áµ¢  Op ğ’³ âˆ˜ Î¹
  Î¹-shuffle = pres-op (Î· unit (MkUnary X shuffle))

  {- and an extraction -}

  extractâ‚ : Carrier ğ’³ â†’ X
  extractâ‚ = Î· counit X

  {- Now our f = projâ‚ is lifted -}

  ğ‘­ : Carrier ğ’³ â†’ Carrier ğ’´
  ğ‘­ = mor (Râ‚ f)

  ğ‘­-pres : ğ‘­ âˆ˜ Op ğ’³  â‰áµ¢  Op ğ’´ âˆ˜ ğ‘­
  ğ‘­-pres = pres-op (Râ‚ f)

  {- Putting these together as in the informal reasoning from earlier;
     namely, â€œ(0)â€, f âˆ˜ Op X â‰ˆ Op Y âˆ˜ f, from earlier.
  -}

  .transport : âˆ€ {x} â†’ extractâ‚‚ (ğ‘­ x) â‰¡ f (extractâ‚ x)
  transport = commute counit f

  {-
       ğ‘­-pres
    â‡’  ğ‘­ âˆ˜ Op ğ’³  â‰áµ¢  Op ğ’´ âˆ˜ ğ‘­
    â‡’  extractâ‚‚ âˆ˜ ğ‘­ âˆ˜ Op ğ’³  â‰áµ¢  extractâ‚‚ âˆ˜ Op ğ’´ âˆ˜ ğ‘­
    â‡’  f âˆ˜ extractâ‚ âˆ˜ Op ğ’³  â‰áµ¢  extractâ‚‚ âˆ˜ Op ğ’´ âˆ˜ ğ‘­       (Z)

    Hmm...call this (Z) and let's see what we get:
       ğ‘­ âˆ˜ Op ğ’³  â‰áµ¢  Op ğ’´ âˆ˜ ğ‘­
    â‡’  Op ğ’´ âˆ˜ ğ‘­ âˆ˜ Op ğ’³  â‰áµ¢  Op ğ’´ âˆ˜ Op ğ’´ âˆ˜ ğ‘­
    â‡’  ğ‘­ âˆ˜ Op ğ’³ âˆ˜ Op ğ’³  â‰áµ¢  Op ğ’´ âˆ˜ Op ğ’´ âˆ˜ ğ‘­
    â‡’  Op ğ’´ âˆ˜ ğ‘­ âˆ˜ Op ğ’³ âˆ˜ Op ğ’³  â‰áµ¢ Op ğ’´ âˆ˜ Op ğ’´ âˆ˜ Op ğ’´ âˆ˜ ğ‘­
    â‡’  ğ‘­ âˆ˜ (Op ğ’³)Â³  â‰áµ¢ (Op ğ’´)Â³ âˆ˜ ğ‘­
  -}

  opx : Carrier ğ’³ â†’ X
  opx = extractâ‚ âˆ˜ Op ğ’³

  formula-zero : extractâ‚‚ âˆ˜ ğ‘­ âˆ˜ Op ğ’³  â‰áµ¢  extractâ‚‚ âˆ˜ Op ğ’´ âˆ˜ ğ‘­
  formula-zero {x} = let open â‰¡-Reasoning in
    begin
      {!!}
    â‰¡âŸ¨ {!!} âŸ©
      {!Î· counit!}
    âˆ

{-
Right : (â„“ : Level) â†’ Functor (Sets â„“) (Unarys â„“)
Right â„“ = record
            { Fâ‚€           = Î» A â†’ MkUnary (Maybe A) (const nothing) -- MkUnary A id
            ; Fâ‚           = Î» f â†’ MkHom (const nothing) refl -- Î» f â†’ MkHom f refl
            ; identity     = {!!} -- Î» _ â†’ refl
            ; homomorphism = {!!} -- Î» _ â†’ refl
            ; F-resp-â‰¡     = {!!} -- Î» eq _ â†’ eq
            }

Adj : (â„“ : Level) â†’ Adjunction (Forget â„“) (Right â„“)
Adj â„“ = record
  { unit = record { Î· = Î» X â†’ MkHom (const nothing) refl
                  ; commute = Î» { (MkHom morâ‚ pres-opâ‚) x â†’ refl } }
  ; counit = record { Î· = Î» X x â†’ {!!}
                    ; commute = Î» f â†’ {!!} }
  ; zig = {!!}
  ; zag = Î» x â†’ {!!}}

-}
\end{code}
%}}}
% Quick Folding Instructions:
% C-c C-s :: show/unfold region
% C-c C-h :: hide/fold region
% C-c C-w :: whole file fold
% C-c C-o :: whole file unfold
%
% Local Variables:
% folded-file: t
% eval: (fold-set-marks "%{{{ " "%}}}")
% eval: (fold-whole-buffer)
% fold-internal-margins: 0
% end:
