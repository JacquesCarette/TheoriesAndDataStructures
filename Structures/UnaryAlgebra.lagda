%{{{ Imports
\begin{code}
module Structures.UnaryAlgebra where

open import Level renaming (suc to lsuc; zero to lzero)

open import Categories.Category   using (Category; module Category)
open import Categories.Functor    using (Functor; Contravariant)
open import Categories.Adjunction using (Adjunction)
open import Categories.Agda       using (Sets)
open import Forget

open import Data.Nat using (â„•; suc)
open import Data.Product using (_Ã—_; _,_ ; Î£; projâ‚; projâ‚‚; uncurry; map)

open import Function2
open import Function

open import EqualityCombinators
\end{code}
%}}}

%{{{ Unary ; Hom ; UnaryAlg ; UnaryCat ; Forget

A single-sorted |Unary| algebra consists of a type along with a function on that type.
For example, the naturals and addition-by-1 or lists and the reverse operation.

Along with functions that preserve the elected operation, such algberas form a category.

\begin{code}
record Unary {â„“} : Set (lsuc â„“) where
  constructor MkUnary
  field
    Carrier : Set â„“
    Op      : Carrier â†’ Carrier

open Unary

record Hom {â„“} (X Y : Unary {â„“}) : Set â„“ where
  constructor MkHom
  field
    mor        :  Carrier X â†’ Carrier Y
    pres-op    :  mor âˆ˜ Op X â‰  Op Y âˆ˜ mor

open Hom

UnaryAlg : {â„“ : Level} â†’ OneSortedAlg â„“
UnaryAlg = record
  { Alg       = Unary
  ; Carrier   = Carrier
  ; Hom       = Hom
  ; mor       = mor
  ; comp      = Î» F G â†’ record
    { mor     =  mor F âˆ˜ mor G
    ; pres-op =  Î» a â†’ â‰¡.trans (â‰¡.cong (mor F) (pres-op G a)) (pres-op F (mor G a))
    }
  ; comp-is-âˆ˜ =  â‰-refl
  ; Id        =  MkHom id â‰-refl
  ; Id-is-id  =  â‰-refl
  }

UnaryCat : {â„“ : Level} â†’ Category (lsuc â„“) â„“ â„“
UnaryCat {â„“} = oneSortedCategory â„“ UnaryAlg

Forget : (â„“ : Level) â†’ Functor (UnaryCat {â„“}) (Sets â„“)
Forget â„“ = mkForgetful â„“ UnaryAlg
\end{code}

%}}}

%{{{ Eventually ; 

We now turn to finding a free unary algebra.

Indeed, we do so by simply not ``interpreting'' the single function symbol that is required
as part of the definition. That is, we form the ``term algebra'' over the signature for
unary algebras.

\begin{code}
data Eventually {â„“} (A : Set â„“) : Set â„“ where
  base : A â†’ Eventually A
  step : Eventually A â†’ Eventually A
\end{code}
The elements of this type are of the form |stepâ¿ (base a)| for |a : A|.

Alternatively, |Eventually A   â‰…   Î£ n âˆ¶ â„• â€¢ A| viz |stepâ¿ (base a) â†” (n , a)|.

Given an unary algebra |(B, ğ’·, ğ“ˆ)| we can interpret the terms of |Eventually A|
where the injection |base| is reified by |ğ’·| and the unary operation |step| is
reified by |ğ“ˆ|.

\begin{code}
âŸ¦_,_âŸ§ : {a b : Level} {A : Set a} {B : Set b} (ğ’· : A â†’ B) (ğ“ˆ : B â†’ B) â†’ Eventually A â†’ B
âŸ¦ ğ’· , ğ“ˆ âŸ§ (base x) = ğ’· x
âŸ¦ ğ’· , ğ“ˆ âŸ§ (step e) = ğ“ˆ (âŸ¦ ğ’· , ğ“ˆ âŸ§ e)
--
-- â€œThe number of ğ“ˆteps is preservedâ€ : âŸ¦ ğ’· , ğ“ˆ âŸ§ âˆ˜ stepâ¿ â‰ ğ“ˆâ¿ âˆ˜ âŸ¦ ğ’· , ğ“ˆ âŸ§

indE : {â„“ a : Level} {A : Set a} {P : Eventually A â†’ Set â„“}
     â†’ ({x : A} â†’ P (base x))
     â†’ ({sofar : Eventually A} â†’ P sofar â†’ P (step sofar))
     â†’ (ev : Eventually A) â†’ P ev
indE {P = P} b s (base x) = b
indE {P = P} b s (step ev) = s (indE {P = P} b s ev)
\end{code}

There's gotta be a way to put these two together into a single operation...

\begin{code}
mapE : {a b : Level} {A : Set a} {B : Set b} â†’ (A â†’ B) â†’ (Eventually A â†’ Eventually B)
mapE f = âŸ¦ base âˆ˜ f , step âŸ§

fromE : âˆ€{â„“} {A : Set â„“} â†’ Eventually A â†’ A
fromE = âŸ¦ id , id âŸ§ -- cf |fromâŠ| ;)

-- More generally,

iterateE : âˆ€ {â„“ } {A : Set â„“} (f : A â†’ A) â†’ Eventually A â†’ A
iterateE f = âŸ¦ id , f âŸ§
--
-- that is, |iterateE f (stepâ¿ base x) â‰ˆ fâ¿ x|

iterateE-nat : âˆ€ {o} {X Y : Unary {o}} (F : Hom X Y)
              â†’ iterateE (Op Y) âˆ˜ mapE (mor F) â‰ mor F âˆ˜ iterateE (Op X)
iterateE-nat F (base x) = â‰¡.refl
iterateE-nat {X = X} {Y = Y} F (step x) = begin
  (iterateE (Op Y) âˆ˜ mapE (mor F) âˆ˜ step) x
    â‰¡âŸ¨ â‰¡.refl âŸ©  -- definitions of mapE and then iterateE
  (Op Y âˆ˜ iterateE (Op Y) âˆ˜ mapE (mor F)) x
    â‰¡âŸ¨ â‰¡.cong (Op Y) (iterateE-nat F x) âŸ©
  (Op Y âˆ˜ mor F âˆ˜ iterateE (Op X)) x
    â‰¡âŸ¨ â‰¡.sym (pres-op F _) âŸ© 
  (mor F âˆ˜ Op X âˆ˜ iterateE (Op X)) x
    â‰¡âŸ¨ â‰¡.refl âŸ© -- definition of iterateE, in reverse
  (mor F âˆ˜ iterateE (Op X) âˆ˜ step) x
     âˆ
     where open â‰¡.â‰¡-Reasoning {A = Carrier Y}

iterateE-mapE-id : âˆ€ {o} {X : Set o} â†’ id {A = Eventually X} â‰ iterateE step âˆ˜ mapE base
iterateE-mapE-id = indE â‰¡.refl (â‰¡.cong step)

mapE-id : âˆ€{a}  {A : Set a} â†’ mapE (id {A = A}) â‰ id
mapE-id = indE â‰¡.refl (â‰¡.cong step)

mapE-âˆ˜ : âˆ€ {o} {X Y Z : Set o} {f : X â†’ Y} {g : Y â†’ Z}
        â†’  mapE (g âˆ˜ f) â‰ mapE g âˆ˜ mapE f
mapE-âˆ˜ = indE â‰¡.refl (â‰¡.cong step)

mapE-cong : âˆ€{o} {A B : Set o} {F G : A â†’ B} â†’ F â‰ G â†’ mapE F â‰ mapE G
mapE-cong eq = indE (â‰¡.cong base âˆ˜ eq $áµ¢) (â‰¡.cong step)

Free : âˆ€ o â†’ Functor (Sets o) (UnaryCat {o})
Free o = record
  { Fâ‚€             =   Î» A â†’ MkUnary (Eventually A) step
  ; Fâ‚             =   Î» f â†’ MkHom (mapE f) â‰-refl
  ; identity       =   mapE-id
  ; homomorphism   =   mapE-âˆ˜
  ; F-resp-â‰¡      =   Î» Fâ‰ˆG â†’ mapE-cong (Î» _ â†’ Fâ‰ˆG)
  }

AdjLeft : âˆ€ o â†’ Adjunction (Free o) (Forget o)
AdjLeft o = record
  { unit     =   record { Î· = Î» _ â†’ base ; commute = Î» _ â†’ â‰¡.refl }
  ; counit   =   record { Î· = Î» { (MkUnary A f) â†’ MkHom (iterateE f) â‰-refl} ; commute = iterateE-nat }
  ; zig      =   iterateE-mapE-id
  ; zag      =   â‰¡.refl
  }
\end{code}

And now for a different way of looking at the same algebra.
We ``mark'' a piece of data with its depth.

\begin{code}
FreeÂ² : âˆ€ o â†’ Functor (Sets o) (UnaryCat {o})
FreeÂ² o = record
  { Fâ‚€ = Î» A â†’ MkUnary (A Ã— â„•) (map id suc)
  ; Fâ‚ = Î» f â†’ MkHom (map f id) (Î» _ â†’ â‰¡.refl)
  ; identity = â‰-refl
  ; homomorphism = â‰-refl
  ; F-resp-â‰¡ = Î» Fâ‰¡G â†’ Î» { (x , n) â†’ â‰¡.congâ‚‚ _,_ (Fâ‰¡G {x}) â‰¡.refl }
  }

iter : {o : Level} {A : Set o} (f : A â†’ A) â†’ A â†’ â„• â†’ A
iter f x â„•.zero = x
iter f x (suc n) = iter f (f x) n

-- important property of iteration
iter-â„• : {o : Level} {A : Set o} {f : A â†’ A} (a : A) (n : â„•) â†’ iter f (f a) n â‰¡ f (iter f a n)
iter-â„• a â„•.zero = â‰¡.refl
iter-â„• {f = f} a (suc n) = iter-â„• {f = f} (f a) n

-- iteration of commutable functions
iter-comm : {o : Level} {B C : Set o} {f : B â†’ C} {g : B â†’ B} {h : C â†’ C} â†’ (f âˆ˜ g â‰ h âˆ˜ f) â†’
  âˆ€ (b : B) (n : â„•) â†’ iter h (f b) n â‰¡ f (iter g b n)
iter-comm eq a â„•.zero = â‰¡.refl
iter-comm {f = f} {g} {h} eq a (suc n) = 
  begin
    iter h (h (f a)) n â‰¡âŸ¨ iter-â„• (f a) n âŸ©
    h (iter h (f a) n) â‰¡âŸ¨ â‰¡.cong h (iter-comm eq a n) âŸ©
    h (f (iter g a n)) â‰¡âŸ¨ â‰¡.sym (eq (iter g a n)) âŸ©
    f (g (iter g a n)) â‰¡âŸ¨ â‰¡.cong f (â‰¡.sym (iter-â„• a n))  âŸ©
    f (iter g (g a) n)
  âˆ
  where open â‰¡.â‰¡-Reasoning

Ã—-induct : {a b c : Level} {A : Set a} {B : A â†’ Set b} {C : Î£ A B â†’ Set c}
  (g : (a : A) (b : B a) â†’ C (a , b)) â†’ ((p : Î£ A B) â†’ C p)
Ã—-induct g = uncurry g

-- There has to be a simpler way, but this will do
zigâ€² : {a : Level} {A : Set a} (x : A) (n : â„•) â†’
  (x , n) â‰¡ iter (map id suc) (x , 0) n
zigâ€² _ â„•.zero = â‰¡.refl
zigâ€² x (suc n) = â‰¡.sym (
  begin
    iter (map id suc) (map id suc (x , 0)) n â‰¡âŸ¨ iter-â„• (x , 0) n âŸ©
    map id suc (iter (map id suc) (x , 0) n) â‰¡âŸ¨ â‰¡.cong (map id suc) (â‰¡.sym (zigâ€² x n)) âŸ©
    map id suc (x , n) â‰¡âŸ¨ â‰¡.refl âŸ©
    (x , suc n)
  âˆ)
  where open â‰¡.â‰¡-Reasoning

AdjLeftÂ² : âˆ€ o â†’ Adjunction (FreeÂ² o) (Forget o)
AdjLeftÂ² o = record
  { unit = record { Î· = Î» _ x â†’ x , 0 ; commute = Î» _ â†’ â‰¡.refl }
  ; counit = record
    { Î· = Î» { (MkUnary A f) â†’ MkHom (uncurry (iter f)) (uncurry iter-â„•) }
    ; commute = Î» { {MkUnary X xÌ‚} {MkUnary Y yÌ‚} (MkHom f pres) â†’ 
      uncurry (iter-comm {f = f} {xÌ‚} {yÌ‚} pres) } }
  ; zig = uncurry zigâ€²
  ; zag = â‰¡.refl
  }
  where
    open â‰¡.â‰¡-Reasoning
\end{code}

% Quick Folding Instructions:
% C-c C-s :: show/unfold region
% C-c C-h :: hide/fold region
% C-c C-w :: whole file fold
% C-c C-o :: whole file unfold
%
% Local Variables:
% folded-file: t
% eval: (fold-set-marks "%{{{ " "%}}}")
% eval: (fold-whole-buffer)
% fold-internal-margins: 0
% end:
